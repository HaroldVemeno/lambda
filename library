Let 0 \fx.x
Let 1 \fx.fx
Let 2 \fx.f(fx)
Let 3 \fx.f(f(fx))
Let 4 \fx.f(f(f(fx)))
Let 5 Succ 4
Let 6 Succ 5
Let 7 Succ 6
Let 8 Succ 7
Let 9 Succ 8
Let 10 Succ 9


Let Succ \nfx.f(nfx)
Let Flip \fxy.fyx
Let P \xyf.fxy

Let T \xy.x
Let True T
Let F \xy.y 
Let False F
Let Not \x.x F T
Let And \xy.xyx 
Let Or  \xy.xxy 
Let Eq \xy.x y (Not y)
Let Xor \xy.x (Not y) y

Let Fst T
Let Snd F

Let I \x.x
Let Y \f.(\a.aa)(\a.f(aa))

Let Const \af.a
Let Wrap \af.fa

Let Add \nm. n Succ m
Let Mul \nmf. n (m f)
Let Pow \nm. m n
Let Is0 \n.n (Const False) True

Let Pred  \n.n (\a. Wrap (a Succ)) (Const 0) I
Let Pred' \n.n (\p. P (p Snd) (Succ (p Snd))) (P 0 0) Fst

Let Sub \nm.m Pred n

Let S \xyz. xz(yz)
Let K \xy.x

Let Fact \n. n (\p. P (p Mul) (Succ (p Snd))) (P 1 1) Fst
Let Fact' Y\rn. Is0 n 1 (Mul n (r (Pred n)))

Let Fib \n. n (\p. P (Add (p Fst) (p Snd)) (p Fst)) (P 1 0) Fst
Let Fib' Y\fn.Is0 n 1 (Is0 (Pred n) 1 (Add (f (Pred n)) (f (Pred (Pred n)))))